#compdef retriever

# Zsh completion for Retriever

_retriever() {
    local -a commands
    local -a options
    local -a worktree_subcommands

    commands=(
        'clone:Select and clone repos from GitHub'
        'fetch:Fetch and rebase selected repos'
        'list:List all repos in the repos directory'
        'worktree:Manage git worktrees'
        'install:Add retriever to your PATH'
        'help:Show help message'
    )

    worktree_subcommands=(
        'list:List all worktrees'
        'add:Create a new worktree'
        'remove:Remove worktrees'
    )

    options=(
        '-d[Directory to store repos]:directory:_files -/'
        '--dir[Directory to store repos]:directory:_files -/'
    )

    _arguments -C \
        $options \
        '1:command:->command' \
        '*::arg:->args'

    case "$state" in
        command)
            _describe -t commands 'retriever commands' commands
            ;;
        args)
            case $words[1] in
                clone|fetch|list)
                    _arguments $options
                    ;;
                worktree)
                    _describe -t worktree_subcommands 'worktree subcommands' worktree_subcommands
                    ;;
            esac
            ;;
    esac
}

_retriever "$@"
